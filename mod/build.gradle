import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	alias libs.plugins.fabric.loom
	alias libs.plugins.jetbrains.kotlin.jvm
}

version = "1.0.0"
group = "top.fifthlight.touchcontroller"

var archivesBaseName = "TouchController"
base {
	archivesName = archivesBaseName
}

repositories {
	maven {
		name = "Terraformers"
		url = "https://maven.terraformersmc.com/"
	}
}

dependencies {
	minecraft(libs.minecraft)
	mappings("net.fabricmc:yarn:${libs.versions.yarn.get()}:v2")
	modImplementation(libs.fabric.loader)

	modImplementation(libs.fabric.api)
	modImplementation(libs.fabric.language.kotlin)
	modImplementation(libs.modmenu)

	implementation(project(":proxy"))
	implementation(libs.koin.core)
	implementation(libs.koin.logger.slf4j)
	implementation(libs.kotlinx.coroutines.core)
}

processResources {
	inputs.property("version", project.version)

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = 21
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}"}
	}
}
